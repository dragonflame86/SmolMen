

prepend function ./tick:
    as @a[team=!red,team=!blue] function ./team/choose

    as @a[scores={smolmen.leave=1.., smolmen.combat=1..}] run tellraw @s {"text":"You have been killed for combat logging."}
    as @a[scores={smolmen.leave=1.., smolmen.combat=1..}] run kill @s

    as @a[team=red,scores={smolmen.leave=1..}] function ./team/red_relog with storage smolmen:const leaders
    as @a[team=blue,scores={smolmen.leave=1..}] function ./team/blue_relog with storage smolmen:const leaders

    unless data storage smolmen:const leaders.red as @r[team=red] function ./team/pick_red_leader
    unless data storage smolmen:const leaders.blue as @r[team=blue] function ./team/pick_blue_leader

    if data storage smolmen:const leaders.red if entity @a[limit=1, team=red] function ./team/check_red_leader with storage smolmen:const leaders.red
    if data storage smolmen:const leaders.blue if entity @a[limit=1, team=blue] function ./team/check_blue_leader with storage smolmen:const leaders.blue

def pick_leader(team: str):
    as @a[team=team,tag=smolmen.leader] function ~/../reset
    data modify storage smolmen:const (f"leaders.{team}.uuid") set from entity @s UUID
    execute summon text_display function ~/get_name:
        data merge entity @s {text: f'{{"selector": "@p[team={team}]" }}'}
        data modify storage smolmen:const (f"leaders.{team}.name") set from entity @s text
        kill @s

    tag @s add smolmen.leader
    tellraw @a ["", {"selector": "@s"}, " is now the leader of the ", {"text": (team[0].upper() + team[1:]), "color": team}, " team"]

    attribute @s minecraft:generic.max_health base set 14

    attribute @s minecraft:generic.scale base set 0.54

    raw attribute @s minecraft:generic.attack_damage modifier add 1234-0-0-0-1 "smolDamage" 0 add_multiplied_total

    loot give @s loot smolmen:attack_leader_horn

    execute if entity @s[team=red] run scoreboard players add #red_leader_deaths smolmen.dummy 1
    execute if entity @s[team=blue] run scoreboard players add #blue_leader_deaths smolmen.dummy 1

    store result score #cmd smolmen.dummy data get entity @s Inventory[{Slot: 103b}].components."minecraft:custom_model_data"
    scoreboard players add #cmd smolmen.dummy 2
    item modify entity @s armor.head {
        "function": "minecraft:set_custom_model_data", 
        "value": {
            "type": "minecraft:score",
            "target": {
                "type": "minecraft:fixed",
                "name": "#cmd"
            },
            "score": "smolmen.dummy"
        }
    }

function ~/pick_red_leader:
    pick_leader("red")

function ~/pick_blue_leader:
    pick_leader("blue")

function ~/check_red_leader:
    $execute unless entity @a[nbt={UUID: $(uuid)},limit=1] as @r[team=red, gamemode=!spectator] run function smolmen:team/pick_red_leader

function ~/check_blue_leader:
    $execute unless entity @a[nbt={UUID: $(uuid)},limit=1] as @r[team=blue, gamemode=!spectator] run function smolmen:team/pick_blue_leader

function ~/reset:
    tag @s remove smolmen.leader
    function ./player/respawn

function ~/red_relog:
    scoreboard players reset @s smolmen.leave
    $execute if entity @s[tag=smolmen.leader,nbt=!{UUID: $(red)}] run function smolmen:team/reset 

function ~/blue_relog:
    scoreboard players reset @s smolmen.leave
    $execute if entity @s[tag=smolmen.leader,nbt=!{UUID: $(blue)}] run function smolmen:team/reset 

append function ./player/respawn:
    if entity @s[tag=smolmen.leader,team=red] as @r[team=red, gamemode=!spectator] function ./team/pick_red_leader 
    if entity @s[tag=smolmen.leader,team=blue] as @r[team=blue, gamemode=!spectator] function ./team/pick_blue_leader

function ~/choose:
    gamemode survival @s

    store result score #red_team smolmen.dummy team list red
    store result score #blue_team smolmen.dummy team list blue

    scoreboard players operation #diff smolmen.dummy = #red_team smolmen.dummy
    scoreboard players operation #diff smolmen.dummy -= #blue_team smolmen.dummy

    if score @s smolmen.team matches 1 function ~/join_red:
        if score #diff smolmen.dummy matches 2.. return run scoreboard players reset @s smolmen.team
        team join red
        tellraw @a ["", {"selector": "@s"},{"text": " has joined the "}, {"text": "Red", "color": "red"}, {"text": " team"}]
        tp @s @e[tag=smolmen.red_base,limit=1]
        at @s spawnpoint @s ~ ~ ~
    
    if score @s smolmen.team matches 2 function ~/joined_blue:
        if score #diff smolmen.dummy matches ..-2 return run scoreboard players reset @s smolmen.team
        team join blue
        tellraw @a ["", {"selector": "@s"},{"text": " has joined the "}, {"text": "Blue", "color": "blue"}, {"text": " team"}]
        tp @s @e[tag=smolmen.blue_base,limit=1]
        at @s spawnpoint @s ~ ~ ~

    if score @s smolmen.team matches 1.. return run function ~/reset:
        effect clear @s
        raw attribute @s minecraft:generic.movement_speed modifier remove 5-1-0-0-1 
        raw attribute @s minecraft:generic.jump_strength modifier remove 5-1-0-0-2 
        scoreboard players reset @s smolmen.team
        recipe give @s *
        function ./player/respawn

    gamemode spectator @s
    tp @s[distance=1..] ~ ~ ~ 0 0

    scoreboard players enable @s smolmen.team

    effect give @s blindness 5 0 true
    effect give @s resistance 1 4 true

    raw attribute @s minecraft:generic.movement_speed modifier add 5-1-0-0-1 no_move -1 add_multiplied_total 
    raw attribute @s minecraft:generic.jump_strength modifier add 5-1-0-0-2 no_jump -1 add_multiplied_total 
    
    title @s times 0 5 0
    title @s title {"text":"Pick a team"}
    title @s subtitle ["",{"text":"Red","color":"red"},{"text":" or "},{"text": "Blue","color":"blue"}]

    tellraw @s [
        ("\n" * 24),
        {"text": ("┏" + "-" * 25 + "\n"), "color": "gray"},
        {"text": "| ", "color": "gray"}, 
        [
            {"text":"Click to join the ","color":"white","clickEvent":{"action":"run_command","value":"/trigger smolmen.team set 1"}},
            {"text":"Red","color":"red","bold":true},
            {"text":" team\n"}
        ],
        {"text": ("|" + "-" * 25 + "\n"), "color": "gray"},
        {"text": "| ", "color": "gray"}, 
        [
            {"text":"Click to join the ","color":"white","clickEvent":{"action":"run_command","value":"/trigger smolmen.team set 2"}},
            {"text":"Blue","color":"blue","bold":true},
            {"text":" team\n"}
        ],
        {"text": ("┗" + "-" * 25), "color": "gray"},
    ]

    if score #diff smolmen.dummy matches 2.. tellraw @s {"text": "Red team is full", "bold": true}
    if score #diff smolmen.dummy matches ..-2 tellraw @s {"text": "Blue team is full", "bold": true}

append function ./check_items:
    raw execute if items entity @s contents #banners[custom_data={leader:1b}] run kill @s

