FUEL_SOURCES = {
    "minecraft:coal": 1600,
    "minecraft:coal_block": 16000 ,
    "minecraft:lava_bucket": 20000,
    "minecraft:blaze_rod": 2400
}

append item_tag ~/fuel {
    "values": list(FUEL_SOURCES.keys())
} 

function ~/tick:
    tag @a[distance=..14] add smolmen.restricted
    unless items block ~ ~3 ~ container.0 * item replace block ~ ~3 ~ container.0 with cobblestone[hide_tooltip={},custom_model_data=1,custom_data={gui:1b}] 
    item replace block ~ ~3 ~ container.2 with air

    store result score #burnTime smolmen.dummy data get block ~ ~3 ~ BurnTime

    if entity @s[tag=!smolmen.running] if score #burnTime smolmen.dummy matches 1.. function ~/../start
    if entity @s[tag=smolmen.running] unless score #burnTime smolmen.dummy matches 1.. function ~/../reset

    if entity @s[tag=smolmen.running] function ~/../run

    as @e[type=creeper, distance=..18, tag=!smolmen.restricted] function ~/../restrict_creepers:
        data merge entity @s {ExplosionRadius: 0}
        tag @s add smolmen.restricted

    kill @e[type=tnt, distance=..18, nbt={fuse: 1s}] 
    kill @e[type=item_display,tag=combatplus.arc_star, distance=..14]

function ~/reset:
    tag @s remove smolmen.running
    setblock ~ ~4 ~ waxed_copper_bulb[lit=false] 

function ~/start:
    tag @s add smolmen.running
    setblock ~ ~4 ~ waxed_copper_bulb[lit=true] 

function ~/run:
    particle block{block_state: {Name: "minecraft:stone"}} ~ ~ ~ 0.25 0 0.25 0 5 normal

    at @s tp @s ~ ~ ~ ~30 ~

    scoreboard players add @s smolmen.dummy 1

    if score @s smolmen.dummy >= @s smolmen.drill_cooldown function ~/../generate 

function ~/generate:
    scoreboard players reset @s smolmen.dummy
    loot insert ~ ~1 ~ mine ~ 128 ~ stone[custom_data={drop_contents:1b}]

function ~/spawn:
    align xyz positioned ~.5 ~ ~.5 function ~/set:
        summon block_display ~ ~ ~ {
            block_state: {Name: "minecraft:hopper"},
            transformation: {
                translation: [-0.75, 0.5, -0.75],
                left_rotation: [0,0,0,1],
                right_rotation: [0,0,0,1],
                scale: [1.5, 1.5, 1.5]
            },
            teleport_duration: 2,
            Tags: ["smolmen.drill_site"]
        }

    setblock ~ 128 ~ yellow_shulker_box[facing=north]{Items:[{id:"minecraft:stone",count:1}],CustomName:'{"text":"Drill Loot"}'}
    setblock ~ 129 ~ barrel{Items:[{id:"minecraft:stone",count:60}],CustomName: '{"text": "Drill Timer, 1 item = 1 second"}'} 

    setblock ~ ~ ~ minecraft:structure_block[mode=load]{mode:"LOAD",name:"minecraft:drill",posX:-8,posY:-1,posZ:-8,powered:0b,rotation:"NONE",sizeX:17,sizeY:7,sizeZ:17}

    function ~/n:
        data merge block ~ ~ ~ {posX: -8, posZ: -8, rotation: "NONE"}
    function ~/e:
        data merge block ~ ~ ~ {posX: 8, posZ: -8, rotation: "CLOCKWISE_90"}
    function ~/s:
        data merge block ~ ~ ~ {posX: 8, posZ: 8, rotation: "CLOCKWISE_180"}
    function ~/w:
        data merge block ~ ~ ~ {posX: -8, posZ: 8, rotation: "COUNTERCLOCKWISE_90"}
    
    $function smolmen:drill/spawn/$(facing)

    setblock ~ ~1 ~ redstone_block
    
    forceload add ~ ~

function ~/1_second:
    store result score @s smolmen.drill_cooldown if items block ~ 129 ~ container.* * 
    scoreboard players operation @s smolmen.drill_cooldown *= #20 smolmen.const

    unless items block ~ ~1 ~ container.* * setblock ~ ~-2 ~ redstone_torch
    if items block ~ ~1 ~ container.* * setblock ~ ~-2 ~ air

append function ./1_second:
    as @e[type=block_display,tag=smolmen.drill_site] at @s function ./drill/1_second
